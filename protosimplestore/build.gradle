plugins {
    id 'com.android.library'
    id 'com.google.protobuf'
    id 'com.diffplug.gradle.spotless'
    id 'org.jetbrains.dokka-android'
}

android {
    buildToolsVersion deps.build.buildToolsVersion
    compileSdkVersion deps.build.compileSdkVersion

    defaultConfig {
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    defaultConfig {
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility deps.build.javaVersion
        targetCompatibility deps.build.javaVersion
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${deps.external.protoc}"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dokka {
  outputFormat = 'gfm'
  outputDirectory = "$rootDir/docs/0.x"
  sourceDirs = android.sourceSets.main.java.srcDirs
}

dependencies {
    implementation project(":simplestore")
    implementation deps.external.findBugs
    api deps.external.protoLite

    testImplementation deps.test.junit
    testImplementation deps.test.truth
    testImplementation deps.test.robolectric

    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.rules

    androidTestUtil deps.test.orchestrator
}

apply plugin: 'com.vanniktech.maven.publish'
